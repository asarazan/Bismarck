/*
 * Copyright (C) 2018 Touchlab, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: "org.jetbrains.kotlin.multiplatform"

group = GROUP
version = VERSION_NAME

def ideaActive = System.getProperty('idea.active') == 'true'

kotlin {
    jvm()
    if (ideaActive) {
        println("Configuring single iosx64 target")
        iosX64("ios") {
            binaries {
                framework {
                    freeCompilerArgs += "-Xobjc-generics"
                }
            }
        }
    } else {
        println("Configuring unified ios target")
        ios() {
            binaries {
                framework {
                    freeCompilerArgs += "-Xobjc-generics"
                }
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin("stdlib-common")
                implementation "co.touchlab:stately-common:$STATELY_VERSION"
                implementation "co.touchlab:stately-concurrency:$STATELY_VERSION"
                implementation "co.touchlab:stately-isolate:$STATELY_VERSION"
                implementation "co.touchlab:stately-iso-collections:$STATELY_VERSION"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$COROUTINES_VERSION"
            }
        }

        commonTest {
            dependencies {
                implementation kotlin("test-common")
                implementation kotlin("test-annotations-common")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$COROUTINES_VERSION"
            }
        }

        jvmMain {
            dependsOn commonMain
            dependencies {
                implementation kotlin("stdlib-jdk8")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$COROUTINES_VERSION"
                api "io.reactivex:rxjava:1.3.8"
                compileOnly "com.squareup.wire:wire-runtime:3.0.1"
                compileOnly "com.google.code.gson:gson:2.8.6"
            }
        }
        jvmTest {
            dependsOn commonTest
            dependencies {
                implementation kotlin("test")
                implementation kotlin("test-junit")
            }
        }

        iosMain {
            dependsOn commonMain
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$COROUTINES_VERSION"
            }
        }

        iosTest {}
    }
}

configurations {
    compileClasspath
}

apply from: "${rootDir}/gradle/gradle-mvn-mpp-push.gradle"
//apply from: "${rootDir}/gradle/npm-publish.gradle"
